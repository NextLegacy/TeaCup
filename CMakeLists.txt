cmake_minimum_required(VERSION 3.30.3)

set(CMAKE_CXX_STANDARD 23)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

#set(CMAKE_INSTALL_PREFIX ${CMAKE_CURRENT_SOURCE_DIR}/install)

project(TeaCup VERSION 0.0.1 LANGUAGES CXX)

include(cmake/dependencies.cmake)
add_subdirectory(TC)

include(InstallRequiredSystemLibraries)
set(CPACK_RESOURCE_FILE_LICENSE ${CMAKE_CURRENT_SOURCE_DIR}/License.txt)
set(CPACK_PACKAGE_VERSION_MAJOR ${PROJECT_VERSION_MAJOR})
set(CPACK_PACKAGE_VERSION_MINOR ${PROJECT_VERSION_MINOR})
set(CPACK_GENERATOR WIX)
set(CPACK_WIX_VERSION 4)
set(CPACK_WIX_UPGRADE_GUID "62350DD9-4ECD-4DFD-95B5-0962B1BEAEFC")

include(GNUInstallDirs)
include(CPack)

install(TARGETS glm-header-only glew glm implot imgui glfw TeaCup
    EXPORT TeaCup
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
    INCLUDES DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
)

install(
    DIRECTORY TC
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
    PATTERN "CMakeLists.txt" EXCLUDE
    PATTERN "*.cpp" EXCLUDE
)

install(
    EXPORT TeaCup
    NAMESPACE TeaCup::
    DESTINATION ${CMAKE_INSTALL_DATAROOTDIR}/TeaCup
    FILE TeaCupConfig.cmake
)

include(CMakePackageConfigHelpers)

write_basic_package_version_file(
    ${CMAKE_CURRENT_BINARY_DIR}/TeaCupConfigVersion.cmake
    VERSION ${PROJECT_VERSION}
    COMPATIBILITY AnyNewerVersion
)

install(
    FILES ${CMAKE_CURRENT_BINARY_DIR}/TeaCupConfigVersion.cmake
    DESTINATION ${CMAKE_INSTALL_DATAROOTDIR}/TeaCup
)